<!DOCTYPE html>
<html>
<head>
	<meta charset="UTF-8">
	 <script src="three.js"></script>
	 <script src="js/CopyShader.js"></script>
	 <script src="js/RGBShiftShader.js"></script>
	 <script src="js/CustomShader.js"></script>
	 <script src="js/ShaderPass.js"></script>
	 <script src="js/MaskPass.js"></script>
	 <script src="js/EffectComposer.js"></script>
	 <script src="js/RenderPass.js"></script>
	 <script src="js/BlendShader.js"></script>
	 <script src="js/HorizontalTiltShiftShader.js"></script>
	 <script src="js/VerticalTiltShiftShader.js"></script>
	 <script src="js/SavePass.js"></script>
	 <script src="js/EKTweener.js"></script>
	 <script src="status.js"></script>
	<title>Document</title>
	<style>
	body,html{
		padding: 0;
		margin: 0;
		overflow: hidden;
	}
	.header,.footer{
		position: absolute;
		width: 100%;
		height: 100px;
		background: #000;
	}
	.header{
		top:0;
	}
	.footer{
		bottom: 0;
	}
	</style>
</head>
<body>
	<div id="container"></div>
	<div class="header"></div>
	<div class="footer"></div>
	<script type="x-shader/x-vertex" id="vertexshader">

		varying float pAlpha;
		varying vec2 colorMapPos;
		varying float alphaIntensity;
		varying float vZoom;
		uniform float fading;
		uniform float time;
		uniform float zoom;
		uniform vec3 globalPos;
		uniform vec3 posOffset;
		uniform vec3 posFieldOffset;
		uniform vec3 cameraVector;
		uniform float dpi;

		const float PI = 3.14159265358979323846264;

		vec3 mod289(vec3 x) {return x - floor(x * (1.0 / 289.0)) * 289.0;}
		vec2 mod289(vec2 x) {return x - floor(x * (1.0 / 289.0)) * 289.0;}
		vec3 permute(vec3 x) {return mod289(((x*34.0)+1.0)*x);}
		float snoise(vec2 v){
			const vec4 C = vec4(0.211324865405187,0.366025403784439,-0.577350269189626,0.024390243902439);
			vec2 i  = floor(v + dot(v, C.yy) );
			vec2 x0 = v -   i + dot(i, C.xx);
			vec2 i1;
			i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
			vec4 x12 = x0.xyxy + C.xxzz;
			x12.xy -= i1;
			i = mod289(i);
			vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))+ i.x + vec3(0.0, i1.x, 1.0 ));
			vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
			m = m*m ;
			m = m*m ;
			vec3 x = 2.0 * fract(p * C.www) - 1.0;
			vec3 h = abs(x) - 0.5;
			vec3 ox = floor(x + 0.5);
			vec3 a0 = x - ox;
			m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
			vec3 g;
			g.x  = a0.x  * x0.x  + h.x  * x0.y;
			g.yz = a0.yz * x12.xz + h.yz * x12.yw;
			return 130.0 * dot(m, g);
		}


		void main() {

		    vec3 refPos = position + posFieldOffset;
		    colorMapPos = refPos.xz;

		    // noise ratio that fixed depends on the particle reference position
		    float fixedPosNoiseRatio = (snoise(refPos.xz * 0.1) + snoise(refPos.xz * 0.005)) * .5;
		    fixedPosNoiseRatio = 1.0 - (cos(fixedPosNoiseRatio * PI) + 1.0) / 2.0 - snoise(refPos.xz * .1) - snoise(refPos.xz * .03);

		    fixedPosNoiseRatio -= snoise(refPos.xz * .003 + 2.1) * 1.5 + snoise(refPos.xz * .3 + 1.1)  + snoise(refPos.xz * .001 + 1.1);

		    fixedPosNoiseRatio *= .5;

		    vec3 pos = position + posOffset;

		    vec4 modelViewPos = modelViewMatrix * vec4( pos + cameraVector, 1.0 );
		    float distanceToCamera = sqrt(modelViewPos.x * modelViewPos.x + modelViewPos.z * modelViewPos.z);

		    float offsetY = snoise(refPos.xz * 0.0013 + 4.0) * - 40.00 + snoise(refPos.xz * 0.0006 + 32.0) * - 90.0;

		    pos.x += (snoise(refPos.xz * 0.3) - .5) * 8.0;
		    pos.y += offsetY + snoise(refPos.xz * 200. + 12.) * 10.0 + (1. - step(0., fixedPosNoiseRatio)) * 99999. ;
		    pos.z += (snoise(refPos.xz * 0.4) - .5) * 8.0;

		    modelViewPos = modelViewMatrix * vec4( pos, 1.0 );

		    distanceToCamera = sqrt(modelViewPos.x * modelViewPos.x + modelViewPos.y * modelViewPos.y + modelViewPos.z * modelViewPos.z);

		    float blinkRatio = step(.1, snoise(refPos.xz)) * snoise(refPos.xz * 1.0 + 30.0 + time * 0.05);

		    pAlpha = clamp(mix(0.5, .6 + .4 * blinkRatio, fixedPosNoiseRatio) * fading * pow(5., clamp(fixedPosNoiseRatio, 0., 1.)), 0., 1.);

		    alphaIntensity = 1.0 + abs(snoise(refPos.xz * 400.0) * .8) * (.3 + blinkRatio * .7);

		    gl_PointSize = mix(500.0, 5000.0, pow(zoom, 2.1))  / distanceToCamera * 6.0 * mix(0.4, 1.0, fixedPosNoiseRatio) * dpi * (.8 + blinkRatio * .3);

		    vZoom = zoom;

		    gl_Position = projectionMatrix * modelViewPos;
		}

		</script>

		<script type="x-shader/x-fragment" id="fragmentshader">
			varying vec2 colorMapPos;
			varying float pAlpha;
			varying float alphaIntensity;
			varying float vZoom;
			// uniform sampler2D tDiffuse;
			uniform sampler2D colorMap;
			uniform float colorMapScale;

			#ifdef USE_FOG
			uniform vec3 fogColor;
			#ifdef FOG_EXP2
			uniform float fogDensity;
			#else
			uniform float fogNear;
			uniform float fogFar;
			#endif
			#endif


			float clampNorm(float val, float min, float max) {
			    return clamp((val - min) / (max - min), 0.0, 1.0);
			}


			void main() {

			    float colorMapScale = 200.0 * colorMapScale;

			    gl_FragColor = texture2D( colorMap, mod(colorMapPos, colorMapScale) / colorMapScale);

			    float distanceToCenter = length(gl_PointCoord.xy - .5) * 2.;

			    gl_FragColor.a = (step(0., distanceToCenter) - step(.25, distanceToCenter)) * 1.;
			    gl_FragColor.a += (step(.25, distanceToCenter) - step(.27, distanceToCenter)) * mix(1., .4, (distanceToCenter - .25) / (.27 - .25));
			    gl_FragColor.a += (step(.27, distanceToCenter) - step(.3, distanceToCenter)) * mix(.4, .15, (distanceToCenter - .27) / (.3 - .27));
			    gl_FragColor.a += (step(.3, distanceToCenter) - step(1., distanceToCenter)) * mix(.15, 0., (distanceToCenter - .3) / (1. - .3));

			    gl_FragColor.a = pow(abs(gl_FragColor.a), alphaIntensity) * pAlpha * ( 1. - clampNorm(vZoom, .75, 1.) * .4);

			#ifdef USE_FOG
			float depth = gl_FragCoord.z / gl_FragCoord.w;
			#ifdef FOG_EXP2
			const float LOG2 = 1.442695;
			float fogFactor = exp2( - fogDensity * fogDensity * depth * depth * LOG2 );
			fogFactor = 1.0 - clamp( fogFactor, 0.0, 1.0 );
			#else
			float fogFactor = smoothstep( fogNear, fogFar, depth );
			#endif
			gl_FragColor = mix( gl_FragColor, vec4( fogColor, gl_FragColor.w ), fogFactor );
			#endif
			}
		</script>
	<script src="index.js"></script>
</body>
</html>